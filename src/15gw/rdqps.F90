!{\src2tex{textfont=tt}}
!!****f* ABINIT/rdqps
!! NAME
!! rdqps
!!
!! FUNCTION
!!  Read a _QPS file
!!
!! COPYRIGHT
!!  Copyright (C) 2005-2008 ABINIT group (FBruneval, MG)
!!  This file is distributed under the terms of the
!!  GNU General Public License, see ~abinit/COPYING
!!  or http://www.gnu.org/copyleft/gpl.txt .
!!
!! INPUTS
!!  nsppol=1 for unpolarized, 2 for spin-polarized.
!!  nspden=Number of SPin-DENsity components. 
!!  nbnds=number of bands considered in the calculation.
!!  gwcalctyp=type of GW calculation (see related input variable). 
!!  Dtfil=datatype containing filenames. 
!!  en(nibz,nbnds,nsppol)= energies for each k-point, band and spin.
!!  Kmesh<Bz_mesh_type> data type with information of the Kmesh.
!!     %nibz=number of irreducible k-points.
!!     %ibz(3,nibz)=reduced coordinates of each irreducible k-point.
!!
!! OUTPUT
!!  nbsc=number of bands used to describe the QP amplitudes 
!!  nscf=number of iterations that have been performed (==0 if we start from a KS calculation)  
!!  en_qp(nibz,nbnds,nsppol)=QP energies at iteration nscf 
!!  m_lda_to_qp(nbnds,nbnds,nibz,nsppol)=matrix giving the decomposition of the QP
!!   wavefunction in the mainfold generated by the KS wavefunctions 
!!   (i.e $ m_lda_to_qp(ib,jb,k,s) := <\psi_{ib,k,s}^{KS}|\psi_{jb,k,s}^{QP}>$
!!  rho_p(nfftot,nspden)=quasiparticle density
!!
!! SIDE EFFECTS
!!
!! NOTES
!!
!! PARENTS
!!      screening,sigma
!!
!! CHILDREN
!!      leave_new,wrtout
!!
!! SOURCE

#if defined HAVE_CONFIG_H
#include "config.h"
#endif

subroutine rdqps(gwcalctyp,Dtfil,Kmesh,nbnds,nspden,nsppol,nscf,nfftot,en,nbsc,en_qp,m_lda_to_qp,rho_p)

 use defs_basis
 use defs_datatypes
 use m_io_tools, only : flush_unit
 use m_errors, only : assert


!This section has been created automatically by the script Abilint (TD). Do not modify the following lines by hand.
 use interfaces_01manage_mpi
!End of the abilint section

 implicit none

!Arguments ------------------------------------
!scalars
 integer,intent(in) :: gwcalctyp,nbnds,nfftot,nspden,nsppol
 integer,intent(out) :: nbsc,nscf
 type(BZ_mesh_type),intent(in) :: Kmesh
 type(Datafiles_type),intent(in) :: Dtfil
!arrays
 real(dp),intent(in) :: en(Kmesh%nibz,nbnds,nsppol)
 real(dp),intent(inout) :: rho_p(nfftot,nspden)
 real(dp),intent(out) :: en_qp(Kmesh%nibz,nbnds,nsppol)
 complex(dpc),intent(out) :: m_lda_to_qp(nbnds,nbnds,Kmesh%nibz,nsppol)

!Local variables-------------------------------
!scalars
 integer :: ib,ii,ik,isppol,nbR,nkR,nsR,unqps
 real(dp) :: uerr
 logical :: lfile,ltest
 character(len=500) :: msg
!arrays
 real(dp) :: kibz(3)
 real(dp),allocatable :: en_tmp(:)
 complex(dpc),allocatable :: mtmp(:,:),utest(:,:)

! *************************************************************************

#if defined DEBUG_MODE 
 write(msg,'(a)')' rdqps : enter'
 call wrtout(std_out,msg,'COLL')
 call flush_unit(std_out)
#endif
 
 !
 ! === Initialize with KS eigenvalues and eigenfunctions ===
 en_qp(:,:,:)=en(:,:,:)
 m_lda_to_qp(:,:,:,:)=czero
 do ib=1,nbnds
  m_lda_to_qp(ib,ib,:,:)=cone
 end do
 !
 ! In case of non self-consistent calculation, exit immediately
 ! TODO this has to be done in a clearer way
 if (gwcalctyp<10) then 
  write(msg,'(2a)')ch10,&
&  ' rdqps : Non self-consistent case, return '
  call wrtout(std_out,msg,'COLL')
  RETURN
 end if
 !
 ! === Self-consistency ===
 ! * Check whether file exists or not.
 unqps=Dtfil%unqps
 write(msg,'(5a)')ch10,&
& ' rdqps: reading QP wavefunctions of the previous step ',ch10,&
& '        looking for file ',TRIM(Dtfil%filqps)
 call wrtout(std_out,msg,'COLL') ; call wrtout(ab_out,msg,'COLL')
 inquire(file=TRIM(Dtfil%filqps),exist=lfile)

 if (.not.lfile) then
  write(msg,'(2a)')&
&  ' file not found, 1st iteration initialized with KS eigenelements ',ch10
  call wrtout(std_out,msg,'COLL') ;call wrtout(ab_out,msg,'COLL')
  nscf=0
  RETURN
 else
  open(unit=unqps,file=TRIM(Dtfil%filqps),form='formatted',status='unknown')
  ! TODO the _QPS file should contain the Abinit header 
  read(unqps,*)nscf
  write(msg,'(a,i4,a)')&
&  ' Number of iteration(s) already performed: ',nscf,ch10
  call wrtout(std_out,msg,'COLL') ; call wrtout(ab_out,msg,'COLL')

  read(unqps,*)nkR
  if (nkR/=Kmesh%nibz) then 
   write(msg,'(6a,i4,a,i4)')ch10,&
&   ' rdqps: ERROR -',ch10,&
&   '  Wrong number of k-points ',ch10,&
&   '  Expected: ',Kmesh%nibz,', Found: ',nkR 
   call wrtout(std_out,msg,'COLL') ; call leave_new('COLL')
  end if

  read(unqps,*)nbR
  nbsc=MIN(nbR,nbnds)
  if (nbsc/=nbnds) then 
   write(msg,'(6a,i4,a,i4)')ch10,&
&   ' rdqps: WARNING - ',ch10,&
&   ' QPS file contains less bands than that used in the present calculation ',ch10,&
&   ' Required: ',nbnds,', Found: ',nbR
   call wrtout(std_out,msg,'COLL') ; call wrtout(ab_out,msg,'COLL')
  end if
  if (nbsc/=nbR) then 
   write(msg,'(6a,i4,a)')ch10,&
&   ' rdqps : COMMENT - ',ch10,&
&   ' the QPS file contains more bands than that used in the present calculation',ch10,&
&   ' only first ',nbR,' bands will be read'
   call wrtout(std_out,msg,'COLL') ; call wrtout(ab_out,msg,'COLL')
  end if

  allocate(mtmp(nbR,nbR),en_tmp(nbR))
  read(unqps,*)nsR
  if (nsR/=nsppol) then 
   write(msg,'(4a,i2,2a,i2)')ch10,&
&   ' rdqps : ERROR - ',ch10,&
&   ' QPS file has been generated with nsppol = ',nsR,ch10,&
&   ' while in the present calculation nsppol = ',nsppol
   call wrtout(std_out,msg,'COLL') ; call leave_new('COLL')
  end if 
  ! 
  ! === Read energies and transformation for each k-point and spin ===
  ! TODO: The format of the QPS file must be standardized !
  do isppol=1,nsppol 
   do ik=1,Kmesh%nibz
    read(unqps,*)kibz(:)
    write(msg,'(a,i5,a,3(f6.3,1x),4x,a,i2)')&
&    ' Reading ik ',ik,')  k = ',kibz(:),' is = ',isppol
    call wrtout(std_out,msg,'COLL')
    if (ANY(ABS(kibz(:)-Kmesh%ibz(:,ik))>0.001)) then 
     write(msg,'(a)')' rdqps: ERROR - wrong k-point read'
     call wrtout(std_out,msg,'COLL') ; call leave_new('COLL')
    end if 
    do ib=1,nbR
     read(unqps,*)en_tmp(ib)
     read(unqps,*)mtmp(:,ib)
    end do 
    m_lda_to_qp(1:nbsc,1:nbsc,ik,isppol)=mtmp(1:nbsc,1:nbsc)
    en_qp(ik,1:nbsc,isppol)=en_tmp(1:nbsc)

    !allocate(utest(nbsc,nbsc))
    !utest=TRANSPOSE(mtmp(1:nbsc,1:nbsc)) !this is just for the buggy gfortran
    !utest=MATMUL(CONJG(utest),mtmp(1:nbsc,1:nbsc))
    !do ii=1,nbsc
    ! utest(ii,ii)=utest(ii,ii)-one
    !end do
    !uerr=MAXVAL(ABS(utest))
    !write(*,*)' uerr = ',uerr
    !deallocate(utest)

   end do !ik
  end do !isppol 
  deallocate(mtmp,en_tmp)
  ! 
  ! === Read the QP density ===
  ! Here take care because the FFT grid must be the same
  ! This problem can be solved if the hdr is reported at the beginning of the QPS file
  read(unqps,*)rho_p(:,:)
  close(unqps)
 end if

 write(msg,'(2a)')ch10,' rdqps : File successfully read '
 call wrtout(std_out,msg,'COLL')
 !Â£call print_QP(nbnds,Kmesh%nibz,nsppol,m_lda_to_qp,en_qp,1,nbsc)

#if defined DEBUG_MODE 
 write(msg,'(a)')' rdqps : exit'
 call wrtout(std_out,msg,'COLL')
 call flush_unit(std_out)
#endif

end subroutine rdqps
!!***
